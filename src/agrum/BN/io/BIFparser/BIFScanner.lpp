/***************************************************************************
 *   Copyright (C) 2005 by Christophe GONZALES and Pierre-Henri WUILLEMIN  *
 *   {prenom.nom}_at_lip6.fr                                               *
 *                                                                         *
 *   This program is free software; you can redistribute it and/or modify  *
 *   it under the terms of the GNU General Public License as published by  *
 *   the Free Software Foundation; either version 2 of the License, or     *
 *   (at your option) any later version.                                   *
 *                                                                         *
 *   This program is distributed in the hope that it will be useful,       *
 *   but WITHOUT ANY WARRANTY; without even the implied warranty of        *
 *   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the         *
 *   GNU General Public License for more details.                          *
 *                                                                         *
 *   You should have received a copy of the GNU General Public License     *
 *   along with this program; if not, write to the                         *
 *   Free Software Foundation, Inc.,                                       *
 *   59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.             *
 ***************************************************************************/
/* $Id: scanner.ll 44 2008-10-23 09:03:19Z tb $ -*- mode: c++ -*- */
%{ /*** C/C++ Declarations ***/
#include <iostream>
#include <vector>

#include <agrum/core/hashTable.h>

#include <agrum/BN/io/BIFparser/BIFScanner.h>

/* import the parser's token type into a local typedef */
typedef gum_bif::BIFParser::token token;
typedef gum_bif::BIFParser::token_type token_type;

int property_caller;

/* By default yylex returns int, we use token_type. Unfortunately yyterminate
 * by default returns 0, which is not of token_type. */
#define yyterminate() return token::END

/* This disables inclusion of unistd.h, which is not available under Visual C++
 * on Win32. The C++ scanner uses STL streams instead. */
#define YY_NO_UNISTD_H

#define string_yylval() yylval->StringVal = new std::string(yytext, yyleng);
#define int_yylval()    yylval->Int = atoi(yytext);
#define float_yylval()  yylval->Float = atof(yytext);

%}

/*** Flex Declarations and Options ***/

/* enable c++ scanner class generation */
%option c++

/* change the name of the scanner class. results in "SkoobFlexLexer" */
%option prefix="BIF"

/* the manual says "somewhat more optimized" */
%option batch

/* enable scanner to generate debug output. disable this for release
 * versions. */
%option debug

/* no support for include files is planned */
%option yywrap nounput

/* enables the use of start condition stacks */
%option stack

/* The following paragraph suffices to track locations accurately. Each time
 * yylex is invoked, the begin position is moved onto the end position. */
%{
#define YY_USER_ACTION  yylloc->columns(yyleng);
%}

NETWORK         "network"
VARIABLE        "variable"
PROBABILITY     "probability"
PROPERTY        "property"
TYPE            "type"
DISCRETE        "discrete"
DEFAULTVALUE    "default"
TABLE           "table"

DIGIT    [0-9]
LETTER   [a-zA-Z_.?-]
WORD     {LETTER}({LETTER}|{DIGIT})*
MODALITY [a-zA-Z0-9_-]+
NET_NAME [a-zA-Z0-9._-]+
INTEGER  [1-9][0-9]*
COMMENT  ("//"[^\n]*|"/*"([^*]*|("*"[^/])*)*"*/")
EXPONENT [eE]([+,-])?([0-9])+
FLOAT    (([0-9])+"."([0-9])*{EXPONENT}?|"."([0-9])+{EXPONENT}?|([0-9])+{EXPONENT})
EQUAL    [[:blank:]]*=[[:blank:]]*
PROP_VAL [^ =;\n]([^;\n])*


%x IN_PROPERTY
%x IN_BEG_PROPERTY
%x IN_DISCRETE
%x IN_BEG_DISCRETE
%x IN_PROBA
%x IN_BEG_PROBA
%x IN_COND_PROBA
%x IN_BEG_NETWORK
%x IN_NETWORK

%%


<*>{COMMENT}   /* remove comments */

<*>{PROPERTY}              { property_caller=YYSTATE; BEGIN (IN_BEG_PROPERTY); }
<IN_BEG_PROPERTY>[^;]*     /* we do nothing */
<IN_PROPERTY>[;]           { BEGIN (property_caller); }


<*>["] { return token::TOK_DOUBLE_QUOTE; }
<*>['] { return token::TOK_QUOTE; }
"(" { return token::TOK_BEG_PARENTHESIS; }
")" { return token::TOK_END_PARENTHESIS; }
";" { return token::TOK_SEMICOLON; }
"=" { return token::TOK_EQUAL; }
"," { return token::TOK_COMMA; }

{NETWORK}                  { BEGIN(IN_BEG_NETWORK); return token::TOK_NETWORK; }
<IN_BEG_NETWORK>{WORD}     { string_yylval(); return token::TOK_WORD; }
<IN_BEG_NETWORK>"{"        { BEGIN (IN_NETWORK); return token::TOK_BEG_CURLY; }
<IN_NETWORK>"}"            { BEGIN (INITIAL); return token::TOK_END_CURLY; }

{VARIABLE}     { return token::TOK_VARIABLE; }
{TYPE}         { return token::TOK_TYPE; }

{DISCRETE}                 { BEGIN (IN_BEG_DISCRETE); return token::TOK_DISCRETE; }
<IN_BEG_DISCRETE>{INTEGER} { int_yylval(); return token::TOK_INTEGER; }
<IN_BEG_DISCRETE>"["       { return token::TOK_BEG_BRACKET; }
<IN_BEG_DISCRETE>"]"       { return token::TOK_END_BRACKET; }
<IN_BEG_DISCRETE>"{"       { BEGIN (IN_DISCRETE); return token::TOK_BEG_CURLY; }
<IN_DISCRETE>{WORD}        { string_yylval(); return token::TOK_MODALITY; }
<IN_DISCRETE>"}"           { BEGIN (INITIAL); return token::TOK_END_CURLY; }

{PROBABILITY}              { BEGIN (IN_BEG_PROBA); return token::TOK_PROBABILITY; }
<IN_BEG_PROBA>"("          { return token::TOK_BEG_PARENTHESIS; }
<IN_BEG_PROBA>")"          { return token::TOK_END_PARENTHESIS; }
<IN_BEG_PROBA>"|"          { return token::TOK_BAR; }
<IN_BEG_PROBA>{WORD}       { string_yylval(); return token::TOK_WORD; }
<IN_BEG_PROBA>{INTEGER}    { int_yylval(); return token::TOK_INTEGER; }
<IN_BEG_PROBA>"{"          { BEGIN (IN_PROBA); return token::TOK_BEG_CURLY; }
<IN_PROBA>"("              { BEGIN (IN_COND_PROBA); return token::TOK_BEG_PARENTHESIS; }
<IN_COND_PROBA>{MODALITY}  { string_yylval(); return token::TOK_MODALITY; }
<IN_COND_PROBA>")"         { BEGIN (IN_PROBA); return token::TOK_END_PARENTHESIS; }
<IN_PROBA>{DEFAULTVALUE}   { return token::TOK_DEFAULT; }
<IN_PROBA>{TABLE}          { return token::TOK_TABLE; }
<IN_PROBA>{FLOAT}          { float_yylval(); return token::TOK_FLOAT; }
<IN_PROBA>{DIGIT}          { int_yylval(); return token::TOK_INTEGER; }
<IN_PROBA>{INTEGER}        { int_yylval(); return token::TOK_INTEGER; }
<IN_PROBA>"}"              { BEGIN (INITIAL); return token::TOK_END_CURLY; }

{FLOAT}    { float_yylval();  return token::TOK_FLOAT; }
{INTEGER}  { int_yylval();    return token::TOK_INTEGER; }
{WORD}     { string_yylval(); return token::TOK_WORD; }
{MODALITY} { string_yylval(); return token::TOK_MODALITY; }

<*>.    /* remove superfluous characters */

%% /*** Additional Code ***/

namespace gum_bif {

  BIFScanner::BIFScanner(std::istream* in, std::ostream* out)
    : BIFFlexLexer(in, out)
  {
    GUM_CONSTRUCTOR( BIFScanner );
  }

  BIFScanner::~BIFScanner()
  {
    GUM_DESTRUCTOR( BIFScanner );
  }

  void BIFScanner::set_debug(bool b)
  {
    yy_flex_debug = b;
  }

}

 /* This implementation of ExampleFlexLexer::yylex() is required to fill the
  * vtable of the class ExampleFlexLexer. We define the scanner's main yylex
  * function via YY_DECL to reside in the Scanner class instead. */

#ifdef yylex
#undef yylex
#endif

int BIFFlexLexer::yylex()
{
    std::cerr << "In BIFFlexLexer::yylex() !" << std::endl;
    return 0;
}

 /* When the scanner receives an end-of-file indication from YY_INPUT, it then
  * checks the yywrap() function. If yywrap() returns false (zero), then it is
  * assumed that the function has gone ahead and set up `yyin' to point to
  * another input file, and scanning continues. If it returns true (non-zero),
  * then the scanner terminates, returning 0 to its caller. */

int BIFFlexLexer::yywrap()
{
    return 1;
}
